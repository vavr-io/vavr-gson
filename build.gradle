plugins {
    id 'java'
    id 'maven'
    id 'jacoco'
    id 'com.bmuschko.nexus' version '2.3.1'
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

group = 'io.vavr'
version = '1.0.0-SNAPSHOT'

repositories {
    mavenCentral()
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots"
    }
}

dependencies {
    compile "io.vavr:vavr:${vavrVersion}"
    compile "com.google.code.gson:gson:${gsonVersion}"
    testCompile "junit:junit:${junitVersion}"
}

task codeCoverageReport(type: JacocoReport) {
    executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")

    sourceSets sourceSets.main

    reports {
        xml.enabled = true
        xml.destination file("${buildDir}/reports/jacoco/report.xml")
        html.enabled = true
        csv.enabled = false
    }
}

codeCoverageReport.dependsOn {
    project.test
}

jar {
    manifest {
        attributes(
                'Automatic-Module-Name': 'io.vavr.gson'
        )
    }
}

modifyPom {
    project {
        name 'Vavr-Gson'
        description 'A set of GSON serialiser/deserialisers for Vavr.io'
        url 'http://vavr.io'
        inceptionYear '2017'

        scm {
            connection 'scm:git:git@github.com:vavr-io/vavr-gson.git'
            developerConnection 'scm:git:git@github.com:vavr-io/vavr-gson.git'
            url 'git@github.com:vavr-io/vavr-gson.git'
        }

        licenses {
            license {
                name 'The Apache Software License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            }
        }

        developers {
            developer {
                id 'ruslansennov'
                name 'Ruslan Sennov'
                email 'ruslan.sennov@gmail.com'
            }
        }
    }
}

nexus {
    sign = !version.endsWith("SNAPSHOT")
}
